#!/usr/bin/perl -w
#
# Copyright (c) 2008 Rainer Clasen
#
# This program is free software; you can redistribute it and/or modify
# it under the terms described in the file LICENSE included in this
# distribution.
#

use strict;
use warnings;
use Workout;
use Getopt::Long;
use I18N::Langinfo qw(langinfo CODESET);

my $charset = langinfo(CODESET);
binmode STDIN, ":encoding($charset)";
binmode STDOUT, ":encoding($charset)";
binmode STDERR, ":encoding($charset)";


# TODO: pod

# concatenate multiple workouts

my $itype;
my $otype;
my $opt_verbose;
my $debug;

my $needhelp;
my $wanthelp;

if( ! GetOptions(
	"debug!"	=> \$debug,
	"help!"		=> \$wanthelp,
	"itype=s"	=> \$itype,
	"otype=s"	=> \$otype,
	"verbose|v!"	=> \$opt_verbose,
)){
	$needhelp++;
}

if( $wanthelp ){
	print <<EOF;
$0 [opt] <output file> <intput file1> ...
concatenate workout files of same type.

Input files must not overlap. They're ordered by time automatically. All
files have to use the same type and recording intervall as the first
specified input file. Meta information (Note, Slope, ...) is taken from
the last input file.

Options:
--itype=<type>      input file type
--otype=<type>      output file type

--debug             enable debuging output
--help              this help
EOF
	exit 0;
}

if( @ARGV <= 2 ){
	print STDERR "in- and output filename required\n";
	$needhelp++;
}

if( $needhelp ){
	print STDERR "use --help for usage info\n";
	exit 1;
}

my $ofname = shift;

my @sources;
foreach my $fname ( @ARGV ){
	$opt_verbose && print "reading ", $fname,"... \n";
	push @sources, Workout::file_read( $fname, {
		ftype	=> $itype,
		debug	=> $debug,
	});
}

$otype ||= Workout::file_type_name( $ofname );
$otype ||= ($sources[0]->filetypes)[0];

my $recint = $sources[0]->recint;

my $dst = Workout::file_new( {
	ftype	=> $otype,
	recint	=> $recint,
	debug	=> $debug,

});

if( $recint && ! $dst->cap_block ){
	my $mod = $sources[0]->time_start % $recint;
	foreach my $in ( @sources ){
		if( $mod != ( $in->time_start % $recint ) ){
			print STDERR "block start time doesn't fit n*recint: ",
				$in->time_start, "\n";
			exit 1;
		}
	}
}

my @sorted = sort {
	$a->time_start <=> $b->time_start;
} @sources;


my $iter = Workout::filter('Concat', shift @sorted, {
	debug	=> $debug,
	sources	=> \@sorted,
});

$iter = Workout::filter('Join', $iter, {
	debug	=> $debug,
	recint	=> $recint,
}) if ! $dst->cap_block;

$dst->from( $iter );

$opt_verbose && print "writing ", $ofname ,"... \n";
$dst->meta_prune;
$dst->write( $ofname );


