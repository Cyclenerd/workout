#!/usr/bin/perl -w

use strict;
use warnings;
use Workout;
use Workout::Athlete;
use Getopt::Long;

# TODO: make filter Join + Resample optional
# TODO: optional filter Smooth 
# TODO: optional filter Pwr calc
my $itype;
my $otype;
my $recint = 5;
my $opt_verbose;
my $debug;

my $needhelp;
my $wanthelp;

if( ! GetOptions(
	"help|h!"	=> \$wanthelp,
	"input|i=s"	=> \$itype,
	"output|o=s"	=> \$otype,
	"recint|r=i"	=> \$recint,
	"verbose|v!"	=> \$opt_verbose,
	"debug!"	=> \$debug,
)){
	$needhelp++;
}

if( $wanthelp ){
	print <<EOF;
$0 [opt] <input file> <output file>
convert workout files between formats

--help              this help
--input|-i=<type>   input file type
--output|-o=<type>  input file type
--recint|-r=<int>   sampling interval (default=5)
--debug             enable debuging output

for now only conversion from .srm to .hrm is supported
EOF
	exit 0;
}

if( @ARGV != 2 ){
	print STDERR "in- and output filename required\n";
	$needhelp++;
}

if( $needhelp ){
	print STDERR "use --help for usage info\n";
	exit 1;
}
	
my( $ifile, $ofile ) = @ARGV;

$itype ||= lc( ($ifile =~ /\.([^.]+)$/)[0] );
$otype ||= lc( ($ofile =~ /\.([^.]+)$/)[0] );

my $ath = Workout::Athlete->new;

my $src = Workout::file_read( $ifile, { 
	ftype	=> $itype,
	debug	=> $debug,
} );
my $join = Workout::filter('Join', $src, {
	debug	=> $debug,
});
my $res = Workout::filter('Resample', $join, { 
	recint => $recint,
	debug	=> $debug,
} );
my $dst = Workout::file_new( { 
	ftype	=> $otype,
	recint	=> $recint, 
	athlete	=> $ath,
	debug	=> $debug,
} );
$dst->from( $res );
$dst->write( $ofile );

print 
"input=", $join->cntin, " ",
"joined=", $join->cntout, " ",
"resampled=", $res->cntout, "\n" if $opt_verbose;
