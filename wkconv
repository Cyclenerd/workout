#!/usr/bin/perl -w

use strict;
use warnings;
use Data::Dumper;
use Workout;
use Workout::Calc;
use Getopt::Long;

my $itype;
my $otype;
my $recint = 5;
my $opt_verbose;

my $needhelp;
my $wanthelp;

if( ! GetOptions(
	"help|h!"	=> \$wanthelp,
	"input|i=s"	=> \$itype,
	"output|o=s"	=> \$otype,
	"recint|r=i"	=> \$recint,
	"verbose|v!"	=> \$opt_verbose,
)){
	$needhelp++;
}

if( $wanthelp ){
	print <<EOF;
$0 [opt] <input file> <output file>
convert workout files between formats

--help              this help
--input|-i=<type>   input file type
--output|-o=<type>  input file type
--recint|-r=<int>   sampling interval (default=5)

for now only conversion from .srm to .hrm is supported
EOF
	exit 0;
}

if( @ARGV != 2 ){
	print STDERR "in- and output filename required\n";
	$needhelp++;
}

if( $needhelp ){
	print STDERR "use --help for usage info\n";
	exit 1;
}
	
my( $ifile, $ofile ) = @ARGV;

$itype ||= lc( ($ifile =~ /\.([^.]+)$/)[0] );
$otype ||= lc( ($ofile =~ /\.([^.]+)$/)[0] );


my $calc = Workout::Calc->new;
my $src = Workout::file_read( $ifile, { 
	ftype	=> $itype,
	calc	=> $calc,
} );
my $join = Workout::filter('Join', $src, { 
	calc => $calc,
} );
my $res = Workout::filter('Resample', $join, { 
	calc => $calc, 
	recint => $recint,
} );
my $dst = Workout::file_new( { 
	ftype	=> $otype,
	calc	=> $calc, 
	recint	=> $recint, 
} );
$dst->from( $res );
$dst->write( $ofile );

print 
"input=", $join->cntin, " ",
"joined=", $join->cntout, " ",
"resampled=", $res->cntout, "\n" if $opt_verbose;
