#!/usr/bin/perl -w
#
# Copyright (c) 2008 Rainer Clasen
# 
# This program is free software; you can redistribute it and/or modify
# it under the terms described in the file LICENSE included in this
# distribution.
#


# TODO: pod

use strict;
use DateTime;
use Getopt::Long;
use Workout;
use MyChart::Gtk;
use Gtk2 '-init';

my $debug;
my $ftp;

my $wanthelp;
my $needhelp;

if( ! GetOptions(
	"ftp=i"		=> \$ftp,
	"debug!"	=> \$debug,
	"help|h!"	=> \$wanthelp,
)){
	$needhelp++;
}

if( $wanthelp ){
	print <<EOF;
$0 [opt] <fnames> ...
plot Pmc Chart (CTL/ATL/TSB)
Options:
--debug             enable debuging output
--ftp=<power>       Functional Threshold Power
--help              this cruft
EOF
	exit 0;
}

if( ! $ftp ){
	print STDERR "missing --ftp\n";
	$needhelp++;
}

if( $needhelp ){
	print STDERR "please use $0 --help for usage info\n";
	exit 1;
}

my( $first, $last );
my %tss;
foreach my $fname ( @ARGV ){
	$debug && print STDERR "reading $fname...\n";
	my $src = Workout::file_read( $fname, { 
		debug	=> $debug, 
	} );
	my $if = Workout::filter( 'FTP', $src, {
		debug	=> $debug,
		ftp	=> $ftp,
	});
	while( $if->next ){ 1 };

	my $date = DateTime->from_epoch(
		epoch	=> $src->time_start,
		time_zone	=> 'local',
	)->truncate( to => 'day' );

	if( ! $first || $date < $first ){
		$first = $date->clone;
	}

	if( ! $last || $date > $last ){
		$last = $date->clone;
	}

	my $d = $date->ymd;
	#$tss{$d} ||= 0;
	$tss{$d} += ($if->tss ||0);
}

if( ! $first ){
	print "no data found\n";
	exit;
}

$debug && print STDERR "building ctl, atl, tsb ", $first->ymd, " to ", $last->ymd, "...\n";
my( @ctl, @atl,  @dat );
my( %min, %max );
while( $first <= $last ){
	my $d = $first->ymd;
	my $tss = $tss{$d}||0;

	unshift @ctl, $tss;
	if( @ctl > 42 ){
		splice @ctl, 42;
	}

	unshift @atl, $tss;
	if( @atl > 7 ){
		splice @atl, 7;
	}

	my $ctl = 0;
	foreach(@ctl){
		$ctl += $_;
	}
	#$debug && print STDERR "ctl: ", scalar(@ctl), " ", $ctl, "\n";
	$ctl /= 42;

	my $atl = 0;
	foreach(@atl){
		$atl += $_;
	}
	#$debug && print STDERR "atl: ", scalar(@atl), " ", $atl, "\n";
	$atl /= 7;

	my $tsb = $ctl - $atl;

	#$debug && print join("\t", $d, map { int($_) } ($tss, $atl, $ctl, $tsb) ),"\n";
	my %r = (
		time	=> $first->epoch,
		date	=> $d,
		tss	=> $tss,
		ctl	=> $ctl,
		atl	=> $atl, 
		tsb	=> $tsb,
	);
	push @dat, \%r;

	foreach my $f (qw( time tss ctl atl tsb )){
		if( ! defined $min{$f} || $min{$f} > $r{$f} ){
			$min{$f} = $r{$f};
		}

		if( ! defined $max{$f} || $max{$f} < $r{$f} ){
			$max{$f} = $r{$f};
		}

	}
	$first->add( days => 1 );
}


my $win = Gtk2::Window->new;
$win->signal_connect( 'destroy' => sub { Gtk2->main_quit } );
$win->signal_connect( 'size-request' => sub {
	my( $self, $req ) = @_;

	$req->width( 600 );
	$req->height( 300 );

	1;
} );

my $s = MyChart::Source->new( {
	list	=> \@dat,
	min	=> \%min,
	max	=> \%max,
} );


my $box = Gtk2::VBox->new;
$win->add( $box );

my $graph = MyChart::Gtk->new;
$graph->add_scale(
	time	=> {
		orientation	=> 0,
		position	=> 1,
		label_fmt	=> sub {
			DateTime->from_epoch(
				epoch		=> $_[0],
				time_zone	=> 'local',
			)->ymd;
		},
		label_rotate	=> 1,
	},
	tsb	=> {
		position	=> 1,
		label_fmt	=> '%d',
	},
	ctl	=> {
		position	=> 2,
		min		=> 0,
		label_fmt	=> '%d',
	},
);
$graph->add_plot({
	xscale	=> 'time',
	yscale	=> 'ctl',
	source	=> $s,
	xcol	=> 'time',
	ycol	=> 'ctl',
},{
	xscale	=> 'time',
	yscale	=> 'ctl',
	source	=> $s,
	xcol	=> 'time',
	ycol	=> 'atl',
},{
	xscale	=> 'time',
	yscale	=> 'tsb',
	source	=> $s,
	xcol	=> 'time',
	ycol	=> 'tsb',
});


$box->pack_start( $graph, 1, 1, 0 );

my $hbox = Gtk2::HBox->new;
$box->pack_start( $hbox, 0, 0, 0 );

# Quit 
my $quit = Gtk2::Button->new( 'Quit' );
$quit->signal_connect( clicked => sub {
	my( $button, $window ) = @_;
	$window->destroy;
}, $win );
$hbox->pack_start( $quit, 1, 0, 0 );




$win->show_all;
Gtk2->main;



