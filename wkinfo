#!/usr/bin/perl -w
#
# Copyright (c) 2008 Rainer Clasen
# 
# This program is free software; you can redistribute it and/or modify
# it under the terms described in the file LICENSE included in this
# distribution.
#


# TODO: pod
# TODO: options for parameters (bike, user)
# TODO: one-line output for tabular lists of several workouts

# TODO: cumulate data from multiple files

use strict;
use DateTime;
use Getopt::Long;
use Workout;
use Workout::Athlete;
use I18N::Langinfo qw(langinfo CODESET);

my $charset = langinfo(CODESET);
binmode STDIN, ":encoding($charset)";
binmode STDOUT, ":encoding($charset)";
binmode STDERR, ":encoding($charset)";


my $itype;
my $debug;
my $ftp;
my $dopwr;
my $domax;
my $domarks;
my $recint;

my $wanthelp;
my $needhelp;

if( ! GetOptions(
	"debug!"	=> \$debug,
	"ftp=i"		=> \$ftp,
	"help!"		=> \$wanthelp,
	"itype=s"	=> \$itype,
	"marker!"	=> \$domarks,
	"maxint!"	=> \$domax,
	"pwr!"		=> \$dopwr,
	"recint=f"	=> \$recint,
)){
	$needhelp++;
}

if( $wanthelp ){
	print <<EOF;
$0 [opt] <fname>
calculate some fancy data from Workout data

Options:
--itype=<type>      input file type

--recint=<sec>      resample before calculatin
--ftp=<w>           threshold power (W)
--pwr               try to calculate missing power
--maxint            calculate per-interval max power

--marker            show per-marker info

--debug             enable debuging output
--help              this cruft
EOF
	exit 0;
}

my $fname = shift;
if( ! $fname ){
	print STDERR "missing input file\n";
	$needhelp++;
}

if( $needhelp ){
	print STDERR "please use $0 --help for usage info\n";
	exit 1;
}

my $src = Workout::file_read( $fname, { 
	debug	=> $debug, 
	ftype	=> $itype, 
} );
my $iter = $src->iterate;

my $ath = $src->can( 'athlete' )
	? $src->athlete
	: Workout::Athlete->new;

$iter = Workout::filter( 'Resample', $iter, {
	debug	=> $debug,
	recint	=> $recint,
}) if $recint;

$iter = my $pwr = Workout::filter( 'Pwr', $iter, { 
	debug	=> $debug,
	athlete => $ath,
}) if $dopwr;

$iter = my $info = Workout::filter( 'Info', $iter, {
	debug	=> $debug,
});

$iter = my $finfo = Workout::filter( 'FTP', $iter, {
	debug	=> $debug,
	ftp	=> $ftp||0,
}) if defined $ftp;

$iter = my $m20 = Workout::filter( 'Max', $iter, {
	debug	=> $debug,
	dur	=> 1200,
}) if defined $domax;

$iter = my $m60 = Workout::filter( 'Max', $iter, {
	debug	=> $debug,
	dur	=> 3600,
}) if defined $domax;

$iter->finish;


if( $info->cntin == 0 ){
	print "file seems empty\n";
	exit;
}

my $sdate = DateTime->from_epoch( 
	epoch		=> $info->time_start,
	time_zone	=> 'local',
);
my $edate = DateTime->from_epoch(
	epoch		=> $info->time_end,
	time_zone	=> 'local',
);

my $fields = $info->fields_used;
my @fields = sort { $a cmp $b } grep { $fields->{$_} } keys %{$fields};

# TODO: zones/inzone

foreach my $attr (qw( athletename note sport circum slope zeropos )){
	next unless $src->can( $attr );
	my $v = $src->$attr or next;

	printf "%-17s%s\n", $attr.':', $v;
}

print 
"chunks:          ", $src->chunk_count, " (", 
	sprintf('%.2f',$info->dur/$src->chunk_count), "s/chunk recint=", 
	$src->recint||0, ")\n",
"used fields:     ", join(',', @fields ), "\n", 
"marker:          ", $src->mark_count, "\n",

"start time:      ", $sdate->datetime, " (", $info->time_start ,")\n",
"end time:        ", $edate->datetime, " (", $info->time_end, ")\n",

"duration:        ", fmtdur( $info->dur ), " h (", int($info->dur/60), "m)\n",
"creeping:        ", fmtdur( $info->dur_creep ), " h (",
	int(($info->dur_creep||0)/60), "m)\n",
"moving:          ", fmtdur( $info->dur_mov ), " h (",
	int(($info->dur_mov||0)/60), "m)\n",
"pedaling:        ", fmtdur( $info->dur_ncad ), " h (",
	int(($info->dur_ncad||0)/60), "m) (",
	sprintf('%.2f', ($info->cad_percent||0) ), "%)\n",

"Temperature min: ", (int($info->temp_min||0) || '?'), " °C\n",
"Temperature max: ", (int($info->temp_max||0) || '?'), " °C\n",
"Temperature avg: ", (int($info->temp_avg||0) || '?'), " °C\n",

"Heartrate min:   ", (int($info->hr_min||0) || '?'), " 1/min\n",
"Heartrate avg:   ", (int($info->hr_avg||0) || '?'), " 1/min\n",
"Heartrate max:   ", (int($info->hr_max||0) || '?'), " 1/min at ",
	fmttime($info->hr_max_time), "\n",

"Cadence avg:     ", (int($info->cad_avg||0) || '?'), " 1/s\n",
"Cadence max:     ", (int($info->cad_max||0) || '?'), " 1/s at ",
	fmttime($info->cad_max_time), "\n",

"start elevation: ", (int($info->ele_start||0) ||'?'), " m\n",
"min elevation:   ", (int($info->ele_min||0) ||'?'), " m\n",
"max elevation:   ", (int($info->ele_max||0) ||'?'), " m at ",
	fmttime($info->ele_max_time), "\n",
"max slope:       ", sprintf( '%.1f', ($info->grad_max||0)), " % at ",
	fmttime($info->grad_max_time), "\n",
"total ascent:    ", (int($info->ascent||0) ||'?'), " m\n",
"total descent:   ", (int($info->descent||0) ||'?'), " m\n",

"total distance:  ", sprintf( '%.2f', ($info->dist||0) / 1000), " km\n",

"max speed:       ", sprintf( '%.2f', ($info->spd_max||0) * 3.6 ), " km/h at ",
	fmttime($info->spd_max_time), "\n",
"avg speed:       ", sprintf( '%.2f', ($info->spd_avg||0) * 3.6 ), " km/h\n",
"max accel:       ", sprintf( '%.2f', ($info->accel_max||0) ), " m/s² at ",
	fmttime($info->accel_max_time), "\n",

"total work:      ", sprintf( '%.2f', ($info->work||0) / 1000 ), " kJ\n",
"avg Power:       ", sprintf( '%.2f', ($info->pwr_avg||0) ), " W\n", # TODO
"max Power:       ", sprintf( '%.2f', ($info->pwr_max||0) ), " W at ", # TODO
	fmttime($info->pwr_max_time), "\n";

$m20 && print
"20m avg Power:   ", sprintf( '%.2f', ($m20->pwr||0) ), " W from ",
	fmttime($m20->stime), " to ",
	fmttime($m20->time), "\n";

$m60 && print
"60m avg Power:   ", sprintf( '%.2f', ($m60->pwr||0) ), " W from ",
	fmttime($m60->stime), " to ",
	fmttime($m60->time), "\n";

$finfo && print 
"Zavg Power:      ", sprintf( '%.2f', ($finfo->apwr||0) ), " W\n",
"norm. Power:     ", sprintf( '%.2f', ($finfo->npwr||0) ), " W (VI=",
	sprintf( '%.2f', ($finfo->vi||0) ), ")\n",
"TSS:             ", sprintf( '%.2f', ($finfo->tss||0) ), " (IF=",
	sprintf( '%.2f', ($finfo->if||0) ), ")\n";

print
"avg Torque:      ", sprintf( '%.2f', ($info->torque_avg||0) ), " Nm\n", # TODO
"max Torque:      ", sprintf( '%.2f', ($info->torque_max||0) ), " Nm at ", # TODO
	fmttime($info->torque_max_time), "\n";

if( $domarks ){
	my $marks = $src->marks;
	my $idx = 0;
	foreach my $mark ( @$marks ){
		my $mi = $mark->info;

		my $sdate = DateTime->from_epoch( 
			epoch		=> $mi->time_start,
			time_zone	=> 'local',
		);
		my $edate = DateTime->from_epoch(
			epoch		=> $mi->time_end,
			time_zone	=> 'local',
		);

print "\nMarker ", $idx++, ":\n",
"idx:             ", $mi->src->ifirst, " - ", $mi->src->ilast, "\n",
"start time:      ", $sdate->datetime, " (", $mi->time_start ," @ ",
	$mi->time_start - $src->time_start ,")\n",
"end time:        ", $edate->datetime, " (", $mi->time_end ," @ ",
	$mi->time_end - $src->time_start ,")\n",

"duration:        ", fmtdur( $mi->dur ), " h (", int($mi->dur/60), "m, ",
		$mi->dur, "s)\n",

"Heartrate min:   ", (int($mi->hr_min||0) || '?'), " 1/min\n",
"Heartrate avg:   ", (int($mi->hr_avg||0) || '?'), " 1/min\n",
"Heartrate max:   ", (int($mi->hr_max||0) || '?'), " 1/min at ",
	fmttime($mi->hr_max_time), "\n",

"Cadence avg:     ", (int($mi->cad_avg||0) || '?'), " 1/s\n",
"Cadence max:     ", (int($mi->cad_max||0) || '?'), " 1/s at ",
	fmttime($mi->cad_max_time), "\n",

"start elevation: ", (int($mi->ele_start||0) ||'?'), " m\n",
"min elevation:   ", (int($mi->ele_min||0) ||'?'), " m\n",
"max elevation:   ", (int($mi->ele_max||0) ||'?'), " m at ",
	fmttime($mi->ele_max_time), "\n",
"total ascent:    ", (int($mi->ascent||0) ||'?'), " m\n",
"total descent:   ", (int($mi->descent||0) ||'?'), " m\n",

"total distance:  ", sprintf( '%.2f', ($mi->dist||0) / 1000), " km\n",

"max speed:       ", sprintf( '%.2f', ($mi->spd_max||0) * 3.6 ), " km/h at ",
	fmttime($mi->spd_max_time), "\n",
"avg speed:       ", sprintf( '%.2f', ($mi->spd_avg||0) * 3.6 ), " km/h\n",
"max accel:       ", sprintf( '%.2f', ($mi->accel_max||0) ), " m/s² at ",
	fmttime($mi->accel_max_time), "\n",

"total work:      ", sprintf( '%.2f', ($mi->work||0) / 1000 ), " kJ\n",
"avg Power:       ", sprintf( '%.2f', ($mi->pwr_avg||0) ), " W\n", # TODO
"max Power:       ", sprintf( '%.2f', ($mi->pwr_max||0) ), " W at ", # TODO
	fmttime($mi->pwr_max_time), "\n";

	}
}

print "\n";
exit 0;

sub fmtdur {
	my $s = shift;
	my $m = int($s / 60); $s %= 60;
	my $h = int($m / 60); $m %= 60;
	sprintf('%d:%02d:%02d', $h, $m, $s);
}

sub fmttime {
	my $t = shift;
	return '?' unless $t;
	my $date = DateTime->from_epoch(
		epoch		=> $t,
		time_zone	=> 'local',
	);
	$date->hms;
}
