#!/usr/bin/perl
#
# Copyright (c) 2008 Rainer Clasen
# 
# This program is free software; you can redistribute it and/or modify
# it under the terms described in the file LICENSE included in this
# distribution.
#

# interactively determine time delta between multiple recordings for one
# workout

# TODO: pod

use strict;
use warnings;
use Getopt::Long;
use Workout;
use Workout::Chart::Delta;

use Gtk2;
use Glib qw/ TRUE FALSE /;
use MyChart::Gtk;

my $delta = 0;
my $recint;
my $field = 'spd';
my $opt_save;
my $opt_gui;
my @itype;

my $wanthelp;
my $needhelp;

if( ! GetOptions(
	"delta|d=i"	=> \$delta,
	"field=s"	=> \$field,
	"gui!"		=> \$opt_gui,
	"help!"		=> \$wanthelp,
	"itype=s"	=> \@itype,
	"recint=f"	=> \$recint,
	"save!"		=> \$opt_save,
)){
	$needhelp++;
}

if( $wanthelp ){
	print <<EOF;
$0 [opt] <fname1> [<fname2>]
interactively determine time delta between multiple recordings for one workout

Options:
--itype=<a>,<b>  file types
--delta=<d>      initial delta for first file
--save           save delta to first file

--gui            interactively determine delta between two files
--recint=<s>     smoothen displayed data (makes it faster, too)
--field=<f>      field to plot (spd, ele, hr, pwr, cad, ...)

--help           this cruft
EOF
	exit 0;
}
$opt_gui = exists $ENV{DISPLAY} unless defined $opt_gui;

# TODO: support multiple fields
if( $field !~ /^(?:ele|spd|cad|hr|pwr)$/ ){
	print STDERR "invalid field: $field\n";
	$needhelp++;
}

my $f1 = shift;
if( ! $f1 ){
	print STDERR "missing filename\n";
	$needhelp++;
}

my $f2;
if( $opt_gui ){
	$f2 = shift;
	if( ! $f2 ){
		print STDERR "missing second filename\n";
		$needhelp++;
	}
}

if( @ARGV ){
	print STDERR "too many files\n";
	$needhelp++;
}


if( $needhelp ){
	print STDERR "please use $0 --help for usage info\n";
	exit 1;
}

@itype = split(/,/,join(',',@itype)) if @itype;

my $wk1 = get_wk( $f1, $itype[0] );

my $gui_canceled;
if( $opt_gui ) {
	my $wk2 = get_wk( $f2, $itype[1] );
	Gtk2->init;
	&show_chart( $field, $wk1, $wk2 );
}

if( $gui_canceled ){
	exit 1;
}

print "$delta\n";

if( $delta && $opt_save ){
	my $store = $wk1->isa('Workout::Store')
		? $wk1 
		: ($wk1->stores)[-1];
	$store->time_add_delta( $delta );
	$store->write( "$f1.wkdelta" );
	unlink $f1 or die "unlink $f1 failed: $!";
	rename "$f1.wkdelta", $f1;
}


sub get_wk {
	my( $fname, $itype ) = @_;

	my $wk = Workout::file_read( $fname, { ftype	=> $itype, });
	$wk = Workout::filter( 'Join', $wk, { recint => $recint });
	$wk = Workout::filter( 'Resample', $wk, { recint => $recint }) if $recint;
	$wk;
}


# TODO: move this to seperate package
sub show_chart {
	my $field = shift;
	#my @workouts = @_;

	my $win = Gtk2::Window->new;
	$win->set_title( 'wkdelta: '. $f1 .' - '. $f2 );
	$win->signal_connect( 'destroy' => sub { Gtk2->main_quit } );
	$win->signal_connect( 'size-request' => sub {
		my( $self, $req ) = @_;

		$req->width( 600 );
		$req->height( 300 );

		1;
	} );



	my $box = Gtk2::VBox->new;
	$win->add( $box );

	# graph
	my $chart = Workout::Chart::Delta->new({
		field	=> $field,
	});
	my $graph = MyChart::Gtk->new(
		chart	=> $chart,
	);

	foreach my $wk ( @_ ){
		$chart->add_workout( $wk );
	}
	$box->pack_start( $graph, 1, 1, 0 );



	my $hbox = Gtk2::HBox->new;
	$box->pack_start( $hbox, 0, 0, 0 );

	# TODO: zoom controls

	# delta adjustment
	my $label = Gtk2::Label->new( 'delta:' );
	$hbox->pack_start( $label, 0, 0, 0 );

	my $dadjust = Gtk2::Adjustment->new(
		$delta, -2000000000, 2000000000,
		1, 60, 0 );
	$chart->set_delta( 0, $delta ) if $delta;

	my $spin = Gtk2::SpinButton->new( $dadjust, 1, 0 );
	$spin->set_activates_default( 1 );
	$hbox->pack_start( $spin, 0, 0, 0 );

	my $set = Gtk2::Button->new( 'show' );
	$set->signal_connect( clicked => sub {
		my( $button, $window ) = @_;
		$chart->set_delta( 0, $dadjust->get_value );
	}, $win );
	$hbox->pack_start( $set, 0, 0, 0 );
	$set->can_default( 1 );
	$win->set_default( $set );


	# Ok 
	my $but_ok = Gtk2::Button->new( 'Ok' );
	$but_ok->signal_connect( clicked => sub {
		my( $button, $window ) = @_;
		$window->destroy;
	}, $win );
	$hbox->pack_start( $but_ok, 1, 0, 0 );


	# Cancel 
	my $but_cancel = Gtk2::Button->new( 'Cancel' );
	$but_cancel->signal_connect( clicked => sub {
		my( $button, $window ) = @_;
		$gui_canceled++;
		$window->destroy;
	}, $win );
	$hbox->pack_start( $but_cancel, 1, 0, 0 );



	$win->show_all;
	Gtk2->main;

	$delta = $dadjust->get_value;
}

