#!/usr/bin/perl
#
# Copyright (c) 2008 Rainer Clasen
# 
# This program is free software; you can redistribute it and/or modify
# it under the terms described in the file LICENSE included in this
# distribution.
#


# example:
# convert srm to hrm
# ... merge gpx ele into hrm when available
# get summary info
# generate body

# TODO: pod


use strict;
use warnings;
use Getopt::Long;
use Workout;
use Workout::Athlete;

my $join;
my $skip;
my $recint;
my $otype;
my $delta = 0;
my @fields;
my $debug;

my $wanthelp;
my $needhelp;

if( ! GetOptions(
	"debug!"	=> \$debug,
	"type=s"	=> \$otype,
	"recint|r=i"	=> \$recint,
	"join|j"	=> \$join,
	"skip!"		=> \$skip,
	"fields|f=s"	=> \@fields,
	"delta|d=i"	=> \$delta,
	"help|h!"	=> \$wanthelp,
)){
	$needhelp++;
}

if( $wanthelp ){
	print <<EOF;
$0 [opt] <fname1> <fname2> <dst-fname>
prepare workout data for mailing it to the trainer

Options:
--debug             enable debuging output
--recint|-r=<int>   output sampling interval (default=fname1's)
--fields|-f=<n,n..> fields to pick from fname2
--delta|-d=<d>      time difference of fname2
--join              join blocks
--skip              skip chunks where essential fields are missing
--help              this cruft
EOF
	exit 0;
}

my $sfname = shift;
if( ! $sfname ){
	print STDERR "missing master file\n";
	$needhelp++;
}

my $gfname = shift;
if( ! $gfname ){
	print STDERR "missing secondary file\n";
	$needhelp++;
}

my $ofname = shift;
if( ! $ofname ){
	print STDERR "missing output file\n";
	$needhelp++;
}

if( @fields ){
	@fields = split(/,/,join(',',@fields));
} else {
	#@fields ||= (qw( time dur dist spd cad hr work pwr));
	@fields = qw( ele );
}
foreach my $f ( @fields ){
	grep /^$f$/, @Workout::Chunk::core_fields
		and next;

	print STDERR "field not supported: $f\n";
	$needhelp++;
}

if( $needhelp ){
	print STDERR "please use $0 --help for usage info\n";
	exit 1;
}

$otype ||= lc( ($ofname =~ /\.([^.]+)$/)[0] );

my $dst = Workout::file_new( { 
	debug	=> $debug,
	ftype	=> $otype,
	#athlete	=> $ath, - see below
} );

if( ! $join and $join = ! $dst->cap_block ){
	$debug && print STDERR "auto-join\n";
}



# read wk1
my $wk1 = Workout::file_read( $sfname, {
	debug	=> $debug,
});
my $it1 = $wk1->iterate;

if( $recint ){
	$dst->recint( $recint );

} elsif( $dst->recint ){
	if( $wk1->recint ){
		$dst->recint( $wk1->recint );
		$recint = $dst->recint if $join;

	} else {
		$recint = $dst->recint;
		$debug && print STDERR "auto-resampling to $recint\n";
	}
}

$it1 = Workout::filter('Timeshift', $it1, { 
	delta	=> $delta,
	debug	=> $debug,
}) if $delta;

$it1 = Workout::filter('Join',  $it1,{
	recint	=> $recint,
	debug	=> $debug,
}) if $join;

$it1 = Workout::filter('Resample', $it1, {
	recint	=> $recint,
	debug	=> $debug,
}) if $recint;
	

# read wk2
my $wk2 = Workout::file_read( $gfname, {
	debug	=> $debug,
});

my $it2 = Workout::filter('Join', $wk2, {
	debug	=> $debug,
});

if( $dst->can( 'athlete' ) ){
	if( $wk2->can( 'athlete' ) ){
		$dst->athlete( $wk2->athlete );

	} elsif( $wk1->can( 'athlete' ) ){
		$dst->athlete( $wk1->athlete );

	} else {
		$dst->athlete( Workout::Athlete->new );

	}
}

my $iter = Workout::filter('Merge', $it2, {
	master	=> $it1, 
	fields	=> \@fields,
	debug	=> $debug,
});

$iter = Workout::filter( 'SkipUndef', $iter, {
	debug	=> $debug,
	fields	=> [ $dst->fields_essential ],
}) if $skip;


# fill dst
$dst->from( $iter );

# write dst
open( my $fh, '>', $ofname )
	or die "open '$ofname': $!";
$dst->write( $fh );
close( $fh );

1;
