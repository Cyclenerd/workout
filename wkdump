#!/usr/bin/perl -w

use strict;
use warnings;
use Data::Dumper;
use Workout;
use Workout::Calc;
use Getopt::Long;

# TODO: allow user to specify fields he wants to be dumped
# TODO: optionally calculate fields

my $itype;
my $needhelp;
my $wanthelp;

if( ! GetOptions(
	"input|i=s"	=> \$itype,
	"help|h!"	=> \$wanthelp,
)){
	$needhelp++;
}

if( $wanthelp ){
	print <<EOF;
$0 [opt] <input file>
dump workout file as tab seperated list

--help              this help
--input|-i=<type>   input file type

for now only .srm files are supported
EOF
	exit 0;
}

if( @ARGV != 1 ){
	print STDERR "you need to specify a single input file\n";
	$needhelp++;
}

if( $needhelp ){
	print STDERR "use --help for usage info\n";
	exit 1;
}

my $ifile = shift;

my $calc = Workout::Calc->new;
my $src = Workout::file_read( $ifile, { ftype => $itype, calc => $calc } );
my $mem = Workout::Store::Memory->new;
$mem->from( $src );

my @fields = $mem->fields_seen;
my $iter = $mem->iterate;

print join("\t", @fields), "\n";
while( my $r = $iter->next ){
	print join("\t", map { $_ || 0 } @$r{@fields}), "\n";
}
