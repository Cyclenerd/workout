#!/usr/bin/perl -w
#
# Copyright (c) 2008 Rainer Clasen
# 
# This program is free software; you can redistribute it and/or modify
# it under the terms described in the file LICENSE included in this
# distribution.
#

# TODO: pod

use strict;
use warnings;
use Workout;
use Getopt::Long;


my $recint;
my $join;
my @fields;

my $debug;
my $itype;

my $needhelp;
my $wanthelp;

if( ! GetOptions(
	"debug!"	=> \$debug,
	"fields=s"	=> \@fields,
	"help!"		=> \$wanthelp,
	"itype=s"	=> \$itype,
	"join!"		=> \$join,
	"recint=f"	=> \$recint,
)){
	$needhelp++;
}

if( $wanthelp ){
	print <<EOF;
$0 [opt] <input file>
dump workout file as tab seperated list

--join              join blocks
--recint=<sec>      resample data to this interval
--fields=<n,n..>    dump specified fields

--debug             enable debuging output
--help              this help

please look at Workout::Chunk for supported fields

EOF
	exit 0;
}

if( @ARGV != 1 ){
	print STDERR "you need to specify a single input file\n";
	$needhelp++;
}

if( $needhelp ){
	print STDERR "use --help for usage info\n";
	exit 1;
}

if( @fields ){
	@fields = split(/,/,join(',',@fields));
} else {
	#@fields ||= (qw( time dur dist spd cad hr work pwr));
	@fields = qw( time dur spd pwr ele );
	# TODO: use fields_io() or fields_used()
}

my $ifname = shift;

my $src = Workout::file_read( $ifname, {
	ftype	=> $itype,
	debug	=> $debug,
} );
my $iter = $src->iterate;

$iter = Workout::filter( 'Join', $iter, {
	debug	=> $debug,
	recint	=> $recint,
} ) if $join;

$iter = Workout::filter( 'Resample', $iter, {
	debug	=> $debug,
	recint	=> $recint,
} ) if $recint;

print join("\t", @fields), "\n";
while( my $r = $iter->next ){
	print join("\t", map { $r->$_ || 0 } @fields), "\n";
}
